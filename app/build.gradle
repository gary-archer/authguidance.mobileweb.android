apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    // Build with latest SDK, which is Android 10
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.authguidance.mobileweb"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        // The scheme value used by AppAuth's RedirectUriReceiverActivity class to receive login responses
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'https'
        ]
    }

    // Be explicit about the JVM version to avoid certain types of warning or error
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // All builds of the app are signed with a self signed key to identify the app for app links
    signingConfigs {
        release {
            storeFile file("${rootDir}/security/app-keystore.jks")
            storePassword "android"
            keyAlias "com.authguidance.mobileweb"
            keyPassword "android"
        }
    }

    // Sign both debug and release builds
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Enable newer data binding
    dataBinding {
        buildFeatures {
            dataBinding = true
        }
    }
}

// Dependency versions
ext {
    nav_version = "2.3.0"
    coroutines_version = '1.3.6'
    appauth_version = '0.7.1'
    gson_version = "2.8.6"
    okio_version = "2.7.0"
    eventbus_version = "3.2.0"
}

dependencies {
    // Base libraries
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Navigation for Single Activity Apps
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Kotlin async support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // The AppAuth library manages OAuth security
    implementation("net.openid:appauth:$appauth_version")

    // Include Chrome Custom tabs for the login window
    implementation 'androidx.browser:browser:1.2.0'

    // JSON serialization
    implementation "com.google.code.gson:gson:$gson_version"

    // Resource file reading
    implementation "com.squareup.okio:okio:$okio_version"

    // Event messages
    implementation "org.greenrobot:eventbus:$eventbus_version"
}
